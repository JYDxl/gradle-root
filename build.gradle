buildscript {
  ext {
    springboot_version = '2.1.3.RELEASE'
    kotlin_version = '1.3.21'
  }

  repositories {
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
  }

  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:$springboot_version")
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version")
    classpath("org.jetbrains.kotlin:kotlin-allopen:$kotlin_version")
  }
}

allprojects {
  group = 'org.github'
  version = '0.0.1-SNAPSHOT'

  ext {
    kotlinx_coroutines_version = '1.1.1'
    mybatis_plus_version = '3.1.0'
    protobuf_version = '3.6.1'
    okhttp3_version = '3.13.1'
    mariadb_version = '2.4.0'
    netty_version = '4.1.33.Final'
    shiro_version = '1.4.0'
    vertx_version = '3.6.3'
    guava_version = '27.0.1-jre'
    commons_lang3_version = '3.8.1'
    commons_codec_version = '1.12'
  }
}

subprojects {
  apply plugin: 'kotlin'
  apply plugin: 'kotlin-spring'
  apply plugin: 'org.springframework.boot'
  apply plugin: 'io.spring.dependency-management'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  bootJar.enabled = false
  test.enabled = false
  jar.enabled = true

  compileKotlin {
    kotlinOptions {
      freeCompilerArgs = ['-Xjsr305=strict']
      jvmTarget = '1.8'
    }
  }

  compileTestKotlin {
    kotlinOptions {
      freeCompilerArgs = ['-Xjsr305=strict']
      jvmTarget = '1.8'
    }
  }

  repositories {
    maven { url 'http://maven.aliyun.com/nexus/content/groups/public/' }
  }

  dependencies {
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
    implementation('org.jetbrains.kotlin:kotlin-stdlib-jdk8')
    implementation('org.jetbrains.kotlin:kotlin-reflect')
    implementation("io.vertx:vertx-web:$vertx_version") { exclude group: 'io.netty' }
    implementation("io.vertx:vertx-lang-kotlin:$vertx_version") { exclude group: 'io.netty' }
    implementation("org.mariadb.jdbc:mariadb-java-client:$mariadb_version")
    implementation("com.google.protobuf:protobuf-java-util:$protobuf_version")
    implementation("com.baomidou:mybatis-plus-boot-starter:$mybatis_plus_version")
    implementation("org.apache.commons:commons-lang3:$commons_lang3_version")
    implementation("commons-codec:commons-codec:$commons_codec_version")
    implementation("com.squareup.okhttp3:okhttp:$okhttp3_version")
    implementation("com.google.guava:guava:$guava_version")
    implementation("io.netty:netty-all:$netty_version")
    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-jdk8:$kotlinx_coroutines_version")

    testImplementation('org.springframework.boot:spring-boot-starter-test') { exclude module: 'android-json' }

    annotationProcessor('org.projectlombok:lombok')
    compileOnly('org.projectlombok:lombok')
    testCompileOnly('org.projectlombok:lombok')
    testAnnotationProcessor('org.projectlombok:lombok')
  }
}
